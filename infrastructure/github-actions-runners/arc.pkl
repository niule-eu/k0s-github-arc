import "@k8s/api/core/v1/Namespace.pkl"
import "@k8s/K8sResource.pkl"
import "@k8s/api/core/v1/PodSpec.pkl"

import "../controllers/arc.pkl" as arc_controller
import "../configs/arc-scale-set.pkl" as arcScaleSet

hidden arc_runner_labels = new Mapping<String, String> {
  ["app.kubernetes.io/name"] = "gha-runner-scale-set"
  ["app.kubernetes.io/part-of"] = "action-runner-controller"
  ["app.kubernetes.io/component"] = "scale-set"
  ["app.kubernetes.io/managed-by"] = "helm"
  ["app.kubernetes.io/version"] = arc_controller.arc_controller_version
}
hidden arc_runner_helm_chart_url = "oci://ghcr.io/actions/actions-runner-controller-charts/gha-runner-scale-set"
hidden arc_runner_helm_chart_name = "gha-runner-scale-set"

local arc_runner_helm_chart_values {
  githubConfigUrl = "https://github.com/niule-eu/platform-engineer-interview3"
  githubConfigSecret = arcScaleSet.arc_runner_secret_name
  maxRunners = 2
  minRunners = 1
  runnerScaleSetName = "radxa-x4"
  containerMode {
    type = "kubernetes"
  }

  template {
    spec = new PodSpec {
      volumes {
        new {
          name = arcScaleSet.arc_runner_hook_job_configmap_name
          configMap {
            name = arcScaleSet.arc_runner_hook_job_configmap_name
          }
        }
        new {
          name = "work"
          ephemeral {
            volumeClaimTemplate {
              spec {
                accessModes {
                  "ReadWriteOnce"
                }
                storageClassName = "local-path"
                resources {
                  requests {
                    ["storage"] = "1Gi"
                  }
                }
              }
            }
          }
        }
      }
      containers {
        new {
          name = "runner"
          image = "ghcr.io/actions/actions-runner:latest"
          command {
            "/home/runner/run.sh"
          }
          volumeMounts {
            new {
              name = arcScaleSet.arc_runner_hook_job_configmap_name
              mountPath = "/home/runner/hooks"
            }
            new {
              name = "work"
              mountPath = "/home/runner/_work"
            }
          }
          env {
            new {
              name = "ARC_APP_CLIENT_ID"
              valueFrom {
                secretKeyRef {
                  name = arcScaleSet.arc_runner_secret_name
                  key = "github_app_client_id"
                }
              }
            }
            new {
              name = "ARC_APP_PRIVATE_KEY"
              valueFrom {
                secretKeyRef {
                  name = arcScaleSet.arc_runner_secret_name
                  key = "github_app_private_key"
                }
              }
            }
            new {
              name = "ACTIONS_RUNNER_HOOK_JOB_STARTED"
              value = "/home/runner/hooks/hook_job_started.sh"
            }
            new {
              name = "ACTIONS_RUNNER_CONTAINER_HOOKS"
              value = "/home/runner/k8s-novolume/index.js"
            }
            new {
              name = "ACTIONS_RUNNER_POD_NAME"
              valueFrom {
                fieldRef {
                  fieldPath = "metadata.name"
                }
              }
            }
            new {
              name = "ACTIONS_RUNNER_REQUIRE_JOB_CONTAINER"
              value = "true"
            }
          }
        }
      }
    }
  }  
}

resources: Listing = new {
  new {
    apiVersion = "source.toolkit.fluxcd.io/v1"
    kind = "OCIRepository"
    metadata {
      name = arc_runner_helm_chart_name
      namespace = "flux-system"
      labels = arc_runner_labels
    }
    spec {
      interval = "5m"
      url = arc_runner_helm_chart_url
      ref {
        semver = arc_controller.arc_controller_version
      }
    }
  }
  new {
    apiVersion = "helm.toolkit.fluxcd.io/v2"
    kind = "HelmRelease"
    metadata {
      name = arc_runner_helm_chart_name
      namespace = arcScaleSet.arc_runner_namespace
      labels = arc_runner_labels
    }
    spec {
      interval = "10m"
      chartRef {
        kind = "OCIRepository"
        name = arc_runner_helm_chart_name
        namespace = "flux-system"
      }
      values = arc_runner_helm_chart_values
    }
  }
}

output {
  value = resources
  renderer = (K8sResource.output.renderer as YamlRenderer) {
    isStream = true
  }
  files {
    ["arc-scale-set.yaml"] {
      value = resources
      renderer = (K8sResource.output.renderer as YamlRenderer) {
        isStream = true
      }
    }
    ["kustomization.yaml"] {
      value = new {        
        apiVersion = "kustomize.config.k8s.io/v1beta1"
        kind = "Kustomization"
        resources = new Listing {
          "arc-scale-set.yaml"
        }
      }
      renderer = (K8sResource.output.renderer as YamlRenderer) {
        isStream = false
      }
    }
  }
}